configfile: "config.yaml"

IN = config["IN"]
SPADES = config["SPADES"]
BIN = config["BIN"]
SCRIPTS = config["SCRIPTS"]
SOFT = config["SOFT"]
QUAST = config["QUAST"]
REF = config["REF"]
K = int(config.get("K", 55))
Kp = K + 1
SAVES = "K{0}/saves/07_before_repeat_resolution/graph_pack".format(K)
MIN_CONTIG_LENGTH = int(config["MIN_CONTIG_LENGTH"])
CAG = config.get("CAG")
THREADS = config.get("THREADS", 24)

SAMPLES, = glob_wildcards(IN + "/{sample,sample\d+}")
SAMPLE_COUNT = len(SAMPLES)

rule all:
    input:   expand("{sample}/binning.log", sample=SAMPLES)
    message: "Dataset has been binned."

rule assemble:
    input:   left = IN + "/{sample}/left.fastq", right = IN + "/{sample}/right.fastq"
    output:  "{sample}/scaffolds.fasta"
    log:  "{sample}/assembly"
    threads: THREADS
    message: "Assembling {wildcards.sample} with SPAdes"
    shell:
        "mkdir -p {wildcards.sample} && "
        "{SPADES}/spades.py --meta -t {threads} -1 {input.left} -2 {input.right} -o {log} && "
        "cp {log}/scaffolds.fasta {output}"

rule descriptions:
    output:  expand("{sample}.desc", sample=SAMPLES)
    message: "Generating sample descriptions"
    shell:   "{SCRIPTS}/dataset_desc_gen.sh {IN}/sample ./"

rule multiplicities:
    input:   expand("{sample}.desc", sample=SAMPLES)
    output:  "kmers.mpl"
    message: "Gathering {Kp}-mer multiplicities from {input}"
    shell:
        "mkdir -p tmp && "
        "export PATH={SOFT}:$PATH && "
        "{BIN}/kmer_multiplicity_counter -k {Kp} -o kmers --mult 2 --sample 3 -d {input}"

rule profile:
    input:   contigs="{sample}/scaffolds.fasta", mpl="kmers.mpl"
    output:  "{sample}/{sample}.id"
    message: "Counting contig abundancies for {wildcards.sample}"
    shell:
        "{BIN}/contig_abundance_counter -k {K} -s {wildcards.sample}/assembly/{SAVES} -c {input.contigs} -n {SAMPLE_COUNT} -m kmers -o {wildcards.sample}/{wildcards.sample} -l {MIN_CONTIG_LENGTH}"

rule canopy_pre:
    input:   expand("{sample}/{sample}.id", sample=SAMPLES)
    output:  "canopy.in"
    message: "Preparing canopy input"
    shell:   "{SCRIPTS}/make_canopy_input.py {output} {input}"

rule canopy:
    input:   rules.canopy_pre.output
    output:  out = "canopy.out", prof = "canopy.prof"
    message: "Running canopy clustering"
    shell:   "{SCRIPTS}/canopy_launch.sh {input} {output.out} {output.prof}"

rule canopy_post:
    input:   rules.canopy.output.out
    output:  expand("{sample}.ann", sample=SAMPLES)
    message: "Preparing raw annotations"
    shell:   "{SCRIPTS}/parse_canopy_out.py {input} ./"

rule propagate:
    input:   "{sample}/scaffolds.fasta", "{sample}.ann"
    output:  "{sample}.ann.prop"
    message: "Propagating annotations for {wildcards.sample}"
    shell:   "{BIN}/annotation_propagator {K} {wildcards.sample}_assembly/{CONFIG} {wildcards.sample}_assembly/{SAVES} {input} {output}"

rule binning:
    input:
        contigs="{sample}/scaffolds.fasta", ann="{sample}.ann",
        left=IN + "/{sample}/left.fastq", right=IN + "/{sample}/right.fastq"
    output:  "{sample}/binning.log"
    params:  "{sample}"
    message: "Propagating annotation & binning reads for {wildcards.sample}"
    #TODO: non-file output?
    shell:
        "{BIN}/prop_binning -k {K} -s {wildcards.sample}/assembly/{SAVES} -c {input.contigs} -a {input.ann} -l {input.left} -r {input.right} -o  binning -n {params} &&"
        "touch {output}"

rule reassemble:
    input:   "{sample}/binning.log"
    output:  "{sample}/reassembly_{cag}.fasta"
    params:  "-1 binning/{cag}/{sample}_1.fastq", "-2 binning/{cag}/{sample}_2.fastq"
    log:     "{sample}/reassembly_{cag}"
    message: "Reassembling reads for {wildcards.cag} from {wildcards.sample}"
    threads: THREADS
    shell:
        "{SPADES}/spades.py --meta -t {threads} {params} -o {log} && "
        "cp {log}/scaffolds.fasta {output}"

#Todo: report error when CAG is unset
rule reassemble_all:
    input:   expand("{sample}/{cag}.fasta", sample=SAMPLES, cag=CAG)
    message: "Reassembling reads for {CAG}"

rule quast:
    input:   "{sample}.fasta"
    output:  "{sample}.cont"
    log:     "{sample}_q"
    message: "Aligning reference for {wildcards.sample} & looking for interesting contigs"
    shell:
        "{QUAST} -R {REF} {input} -o {log} &&"
        "{SCRIPTS}/filter_nucmer.py {log} 70 > {output}"

rule pca:
    input:   "canopy.in", "canopy.out", "{sample}.cont"
    output:  "{sample}.png"
    message: "Doing some visualization"
    shell:
        "Rscript {SCRIPTS}/pca.R {input} {output}"

rule stats_all:
    input:   expand("{sample}.png", sample=SAMPLES)
    message: "Running stats"
