configfile: "config.yaml"

from itertools import chain
from functools import partial
import os.path

from scripts.common import detect_reads, fill_default_values

#Config parameters
fill_default_values(config)

#TODO: check if modern Snakemake allows dictionaries in string splicing
IN = config["data"]
ASSEMBLER = config["assembly"]["assembler"]
ASSEMBLER_DIR = config["assembly"]["dir"]
REASSEMBLER_DIR = config["reassembly"]["dir"]
BIN = config["bin"]
SCRIPTS = config["scripts"]
SOFT = config["soft"]
ASSEMBLY_K = config["assembly"]["k"]
PROFILE_K = config["profile"]["k"]
PROFILER = config["profile"]["profiler"]
SPLIT_LENGTH = config["profile"]["split"]
MIN_CONTIG_LENGTH = config["binning"]["min_length"]
MIN_NONZEROES = config["binning"]["min_nonzeroes"]
THREADS = config["threads"]
BINNER = config["binning"]["binner"]

#Autodetect samples and their reads
#Check that sample names are consecutive and all are presented
SAMPLE_DIRS = set(glob_wildcards(os.path.join(IN, "{sample,sample\d+}"))[0])
SAMPLE_COUNT = len(SAMPLE_DIRS)
SAMPLES = list()
for i in range(1, SAMPLE_COUNT + 1):
    sample_name = "sample" + str(i)
    if sample_name not in SAMPLE_DIRS:
        raise WorkflowError("Samples must be consecutive; missing " + sample_name)
    SAMPLES.append(sample_name)

SAMPLE_READS = dict(map(lambda sample: (sample, detect_reads(os.path.join(IN, sample))), SAMPLES))

NAME_TEMPLATE = "(\w+\.?)?\d+"

#Group samples
GROUP_SAMPLES = config["assembly"]["groups"]
USED_SAMPLES = set(chain(*GROUP_SAMPLES))
#TODO: double-check
#Replace the wildcard group with unused samples
if GROUP_SAMPLES and GROUP_SAMPLES[-1] == "*":
    GROUP_SAMPLES[-1] = [sample for sample in SAMPLES if sample not in USED_SAMPLES]
#Otherwise, add a single-sample group from the rest of the samples
else:
    for sample in SAMPLES:
        if sample not in USED_SAMPLES:
            GROUP_SAMPLES.append([sample])

#Dictionary: {group name: [samples of the group]}
#Can be iterated to retrieve all group names
GROUPS = dict()
group_id = 1
for group in GROUP_SAMPLES:
    if len(group) == 1:
        key = group[0]
    else:
        key = "group" + str(group_id)
        #SAMPLE_READS[key] = ["reads/{}/{}.fastq".format(key, dir) for dir in ["left", "right"]]
        SAMPLE_READS[key] = ([SAMPLE_READS[s][0] for s in group], [SAMPLE_READS[s][1] for s in group])
        group_id += 1
    GROUPS[key] = group

#Helpers for locating input files
def sample_reads(dir, wildcards):
    return SAMPLE_READS[wildcards["sample"]][dir]

def group_reads(dir, wildcards):
    return SAMPLE_READS[wildcards["group"]][dir]

#Returns the filepath with left/right reads for a sample or list of them for a group, used as Snakemake input
left_sample_reads  = partial(sample_reads, 0)
right_sample_reads = partial(sample_reads, 1)

left_reads  = partial(group_reads, 0)
right_reads = partial(group_reads, 1)

rule combine_contigs:
    input:   expand("{{dir}}/{group}.fasta", group=GROUPS)
    output:  "{dir}/all.fasta"
    message: "Combine all contigs from {wildcards.dir}"
    shell:   "{SCRIPTS}/combine_contigs.py -r {input} > {output}"

rule combine_annotation:
    input:   expand("{{stage}}/annotation/{group}.ann", group=GROUPS)
    output:  "{stage}/annotation/all.ann"
    message: "Combine all annotation on {wildcards.stage}"
    run:
        shell("rm -f {output}")
        for sample_ann in input:
            sample, _ = os.path.splitext(os.path.basename(sample_ann))
            shell("sed -e 's/^/{sample}-/' {sample_ann} >> {output}")
