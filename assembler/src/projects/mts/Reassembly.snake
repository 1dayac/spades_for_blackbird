include: "Common.snake"

import yaml

READ_LENGTH = config.get("read_length", 100) #TODO: auto-detect default value

BINS, = glob_wildcards("binning/{{bin,{}}}.info".format(NAME_TEMPLATE))
if not BINS:
    raise WorkflowError("No bins detected!")

rule reassemble_all:
    input:   expand("reassembly/bins/{bin}.fasta", bin=BINS)
    message: "Reassembly finished."

rule reassembly_config:
    input:   "binning/{bin}.info"
    output:  "reassembly/{bin}.yaml"
    message: "Generating config file for reassembly of {wildcards.bin}"
    run:
        with open(output[0], "w") as outfile:
            conf = {"k": PROFILE_K, "sample_cnt": SAMPLE_COUNT,
                    "kmer_mult": "profile/mts/kmers",
                    "bin": wildcards.bin, "bin_prof": "binning/{}/bins.prof".format(BINNER),
                    "edges_sqn": "reassembly/{}_edges.fasta".format(wildcards.bin),
                    "edges_mpl": "reassembly/{}_edges.mpl".format(wildcards.bin),
                    "edge_fragments_mpl": "reassembly/{}_edges_frag.mpl".format(wildcards.bin),
                    "frag_size": SPLIT_LENGTH, "min_len": 100}
            yaml.dump(conf, outfile)

#For bin reassembly, we choose only matching reads from samples which were marked with +
#in the corresponding {bin}.info file
BIN_READS = dict()
for bin in BINS:
    with open("binning/{}.info".format(bin)) as samples_info:
        samples = []
        for line in samples_info:
            sample_data = line.split()
            if sample_data[0][0] == "+":
                sample = sample_data[0][1:]
                if not os.path.exists("binning/{}/{}_1.fastq.gz".format(bin, sample)):
                    print("\033[33mWarning: {} contains no reads for {}\033[0m".format(sample, bin))
                    continue
                samples.append(sample)
        BIN_READS[bin] = (["binning/{}/{}_1.fastq.gz".format(bin, sample) for sample in samples],
                          ["binning/{}/{}_2.fastq.gz".format(bin, sample) for sample in samples])

def bin_reads(dir, wildcards):
    return BIN_READS[wildcards["bin"]][dir]

#Returns the filepath with left/right reads for a sample or list of them for a group, used as Snakemake input
left_bin_reads, right_bin_reads = reads_input(bin_reads)

rule reassemble:
    input:   info="binning/{bin}.info", config="reassembly/{bin}.yaml"
    output:  "reassembly/bins/{bin}.fasta"
    params:  out="reassembly/{bin}",
             left=lambda w: " ".join(expand("--pe1-1 {r}", r=left_bin_reads(w))),
             right=lambda w: " ".join(expand("--pe1-2 {r}", r=right_bin_reads(w))),
    log:     "reassembly/{bin}.log"
    threads: THREADS
    message: "Reassembling reads for {wildcards.bin}"
    run:
        COV_THRESHOLD = 0.3
        subtotal = 0
        with open(input.info) as samples_info:
            for line in samples_info:
                sample_data = line.split()
                if sample_data[0][0] == "+":
                    subtotal += float(sample_data[1])
        bin_cov = COV_THRESHOLD * subtotal * READ_LENGTH / (READ_LENGTH - PROFILE_K)
        shell("{REASSEMBLER_DIR}/spades.py --only-assembler -t {threads}"
             " {params.left} {params.right} --pe1-ff -o {params.out}"
             " --hidden-cov-cutoff {bin_cov} --series-analysis {input.config}"
             " >{log} 2>&1 && cp {params.out}/scaffolds.fasta {output}")
